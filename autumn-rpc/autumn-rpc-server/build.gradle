group 'cn.imaq'
version '0.2'

apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'log4j:log4j:1.2.17'
    compile 'org.slf4j:slf4j-log4j12:1.7.25'
    compile 'org.rapidoid:rapidoid-http-fast:5.3.5'
    compile 'io.github.lukehutch:fast-classpath-scanner:2.4.2'
    compile 'com.esotericsoftware:reflectasm:1.11.3'
    compileOnly 'org.projectlombok:lombok:1.16.18'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

apply plugin: 'maven'
apply plugin: 'signing'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'AutumnRPC'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Lightweight HTTP/JSON RPC framework'
                url 'https://github.com/DeepAQ/Autumn'

                scm {
                    connection 'scm:git:git://github.com/DeepAQ/Autumn.git'
                    developerConnection 'scm:git:ssh://github.com:DeepAQ/Autumn.git'
                    url 'http://github.com/DeepAQ/Autumn/tree/master'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'DeepAQ'
                        name 'Monkey Liang'
                        email 'i@aq741.com'
                    }
                }
            }
        }
    }
}